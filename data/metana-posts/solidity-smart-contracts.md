



How to Write Solidity Smart Contracts - Metana

















































































 












Skip links* [Skip to primary navigation](#primary-nav)
* [Skip to content](#lqd-site-content)



 





 


 




Call us +1 415 416 0800 


















 [![Metana](https://metana.io/wp-content/uploads/2022/07/Metana-Logo.png)](https://metana.io/) 








* [Solidity BootcampHot üî•](https://metana.io/web3-solidity-bootcamp-ethereum-blockchain/)
* [Bootcamps](#) 








###### Web3

 






### Web3 Solidity Bootcamp



Most Popular üî•
The most advanced Solidity curriculum on the internet











### Full Stack Web3 Beginner Bootcamp



New ‚ú® | Beginner üë®‚Äçüíª
Starting off with web development and seamlessly integrating web3 development











### Web3 Rust Bootcamp



Become an expert Solana blockchain developer with one course!














###### Coding + Career Growth

 




 








### Software Engineering Bootcamp



Become a Software Engineer - Balance everyday life commitments & launch your coding career in as little as 16 weeks











### Metana's JobCamp‚Ñ¢Ô∏è



Learn to make a lasting first impressions, network effectively & search for jobs with confidence.















 ![](https://metana.io/wp-content/uploads/2023/10/Screenshot-2023-10-25-at-7.51.33‚ÄØPM-300x61.png) 




 Rated
the Best

 





 Ranked as the industry's premier Web3 bootcamp with a stellar 4.8/5 star rating on Course Report.

 




[Read more reviews here](/reviews/)












[Still Unsure?](https://formless.ai/c/6bPi6ASGJ4ge)
* [Resources](#)
	+ [Reviews](https://metana.io/reviews/)
	+ [Metana‚Äôs Job Guarantee](https://metana.io/job-guarantee/)
	+ [Tuition](https://metana.io/tuition/)
	+ [Events](https://metana.io/events/)
* [Blog](https://metana.io/blog/)

 





[Apply now](https://metana.typeform.com/general)






















 







 [![Metana](https://metana.io/wp-content/uploads/2022/07/Metana-Logo.png)](https://metana.io/) 









Metana Bootcamps
----------------

 






* [Solidity BootcampHot üî•](https://metana.io/web3-solidity-bootcamp-ethereum-blockchain/)
* [Full Stack Web3 Bootcamp](https://metana.io/web3-beginner-bootcamp/)
* [Rust Bootcamp](https://metana.io/web3-rust-bootcamp-solana-blockchain/)
* [Cybersecurity Bootcamp](https://metana.io/cybersecurity-bootcamp/)
* [AI & Machine Learning Bootcamp](https://metana.io/ai-machine-learning-bootcamp/)
* [Data Analytics Bootcamp](https://metana.io/data-analytics-bootcamp/)
* [Data Science Bootcamp](https://metana.io/data-science-bootcamp/)
* [Full Stack Bootcamp](https://metana.io/full-stack-software-engineer-bootcamp/)
* [Jobcamp](https://metana.io/jobcamp/)

 














Menu Items
----------

 






* [Blog](https://metana.io/blog/)
* [Metana‚Äôs Job Guarantee](https://metana.io/job-guarantee/)
* [Refer a Friend](https://metana.io/refer/)
* [Tuition](https://metana.io/tuition/)
* [Withdrawal and Refund Policy](https://metana.io/withdrawal-and-refund-policy/)

 









 








 [[email¬†protected]](/cdn-cgi/l/email-protection)

 




 

[Facebook](https://www.facebook.com/metanahq) 


[Twitter](https://twitter.com/metanahq) 


[Linkedin](https://www.linkedin.com/school/metana/) 


























 [![Metana](https://metana.io/wp-content/uploads/2022/07/Metana-Logo.png)](https://metana.io/) 




[Apply now](https://metana.typeform.com/general)
























 
#### Table of Contents





 







How to Write Solidity Smart Contracts
=====================================

 



 * [Ali Kanso](https://metana.io/blog/author/aii-kanso/)
* [October 19, 2023](https://metana.io/blog/2023/10/19/)
* [Blockchain](https://metana.io/blog/category/blockchain/), [Smart Contracts](https://metana.io/blog/category/smart-contracts/)














Smart contracts are like digital wizards‚Äîself-executing agreements with predefined rules, all meticulously encoded. They‚Äôre the enchanting core of blockchain technology, promising a world of transparency and security. Among the enchanters, Ethereum stands tall as one of the foremost blockchain platforms, and it wields a magical language called Solidity to craft these digital spells.


In this guide, we‚Äôll be your trusty guides on this mystical journey, unraveling the arcane secrets of Solidity, and together, we‚Äôll conjure your very first one of **solidity [smart](https://metana.io/blog/what-is-a-smart-contract-understand-contracts-on-the-blockchain/)** [**contracts**](https://metana.io/blog/what-is-a-smart-contract-understand-contracts-on-the-blockchain/) in just 10 enchanting steps. So, prepare to embark on an adventure where code meets conjuration!


What are the Prerequisites?
---------------------------


Before we delve into the enchanting world of **Solidity Smart Contracts**, there are a few things to consider:


1. **Basic Programming Knowledge:** To navigate this magical realm, it‚Äôs advisable to have a foundation in programming, preferably in a language like JavaScript or something similar to it.
2. **Development Environment:** You‚Äôll need a well-prepared development environment equipped with the Solidity compiler. For a swift initiation, we recommend the [Remix IDE](https://remix.ethereum.org/), which is a powerful environment developed by the Ethereum team.
3. **Blockchain Understanding (Optional):** While not an absolute requirement, a basic comprehension of blockchain and Ethereum can be your guiding star.


With these prerequisites in place, you‚Äôre poised to embark on your journey into the realm of Solidity and smart contracts. Prepare to weave your digital spells!


How to Write Solidity Smart Contracts?
--------------------------------------


In this enchanting journey, we‚Äôll embark on a quest to create a delightful contract‚Äîa ‚ÄúMessage of the Day‚Äù oracle. Our contract will evolve in complexity as we proceed, allowing us to unearth a myriad of Solidity‚Äôs secrets and capabilities.


So, fasten your seatbelts as we set out to explore the fascinating world of Solidity, one spell at a time!


And remember, while this journey may stretch on a bit, fear not! We‚Äôre committed to covering every fundamental aspect of Solidity, ensuring you‚Äôre well-prepared for your magical coding endeavors.


### 1. Setting Up Solidity


Our initial step is to configure the Remix environment, ensuring it‚Äôs primed for our journey into Solidity. Here‚Äôs how you can do it:


1.Click on the ‚ÄúWorkspaces‚Äù drop-down located at the top left corner. 


![remix idesolidity smart contractssmart contracts with solidity](https://metana.io/wp-content/uploads/2023/10/1-1024x500.png)
2. Select ‚ÄúCreate a New Workspace.‚Äù


3. Opt for the ‚ÄúBlank‚Äù template; you have the liberty to assign it any name that appeals to you, such as ‚ÄúIntro-to-Solidity.‚Äù


4. Below the workspace, locate the file icon and click on it to generate a new file. This file can be named as you prefer, but it must end in ‚Äú.sol‚Äù. For our purposes, we shall name it ‚Äúmessage.sol.‚Äù 


![](https://metana.io/wp-content/uploads/2023/10/2-1024x500.png)
With this configuration in place, we stand ready to embark on our coding expedition!


### 2. The Basics



```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract MessageOfTheDay {
    string public message = "Be kind!";
}

```

Every Solidity smart contract starts with two crucial elements at the top: its license and compiler version. While the license isn‚Äôt always mandatory, it‚Äôs a good practice to include it as it clarifies how your contract can be used for commercial purposes. As for the compiler version, it‚Äôs necessary because it provides essential information to the [Ethereum Virtual Machine (EVM)](https://ethereum.org/en/developers/docs/evm/) on how to process your code. Newer compiler versions often bring performance enhancements and security updates, making it wise to specify the most up-to-date version when initiating a new project.


Following this, we declare our contract which is named ‚ÄúMessageOfTheDay.‚Äù Within it, we define our message, which currently reads as ‚ÄúBe kind!‚Äù Take a look at how we structure this message declaration with the following syntax: **<type> <visibility> <name>**. This represents the template for defining any [variable in Solidity](https://metana.io/blog/solidity-variables-types-and-uses/). The name can be customized to your preference, as long as it doesn‚Äôt clash with [Solidity‚Äôs reserved keywords](https://www.geeksforgeeks.org/keywords-in-solidity/). As for the type and visibility themselves, we‚Äôll delve deeper into them in a bit.


  
Before we proceed, let‚Äôs ensure you know how to compile a contract within Remix. To compile your contract, you have two options:


* Click on the play button located above the contract code.
* Press ‚ÄúCtrl + S‚Äù as a shortcut.


A successful compilation will be indicated by a green check-mark next to the compiler icon on the left-hand side of the screen.


Once your contract is compiled, you can deploy it. Click on the deploy icon on the left, which will open a tab displaying your contract‚Äôs name and a deploy button beneath it. 


![](https://metana.io/wp-content/uploads/2023/10/3-1-1024x500.png)
After clicking ‚ÄúDeploy,‚Äù your contract will appear under the ‚ÄúDeployed Contracts‚Äù section. Click on the ‚ÄúMessage‚Äù button within it and take note of what appears beneath the button‚Äîit‚Äôs our message!


![](https://metana.io/wp-content/uploads/2023/10/4-1024x500.png)
As it stands, our message is hard-coded into the contract. Let‚Äôs now evolve it to introduce dynamic behavior!


### 3. Initializing The Contract



```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract MessageOfTheDay {
    string public message;

    constructor(string memory initialMessage) {
        message = initialMessage;
    }
}

```

In this section, we introduce two critical concepts: the constructor and the memory keyword. Let‚Äôs explore their significance:


The Constructor: Think of the constructor as the grand conductor that orchestrates the contract‚Äôs initial symphony. It‚Äôs the function that runs when you deploy a contract, responsible for executing code right at the contract‚Äôs creation. Typically, constructors are used to initialize contract variables.


The Memory Keyword: Now, the memory keyword is a unique gem in the realm of Solidity. It comes into play with data types that are not [primitive](https://solidity-by-example.org/primitives/) and serves as a directive to the program regarding where these non-primitive variables reside. In this case, ‚ÄúinitialMessage‚Äù finds its home in the program‚Äôs memory and will cease to exist once the constructor function completes its enchantment.


After this, we perform a simple task‚Äîassigning the message variable to our initial message. To see the magic in action, compile and deploy the program once more. You‚Äôll notice an input that allows you to provide your initial message.


But our message is not done evolving; we need a way to change it beyond the confines of the constructor, and that‚Äôs where functions take center stage.


### 4. Change is Good



```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract MessageOfTheDay {
    string public message;

    constructor(string memory initialMessage) {
        message = initialMessage;
    }

    function setMessage(string memory newMessage) external {
        message = newMessage;
    }
}

```

As you can see, there‚Äôs nothing special about what our function does, it just takes a bunch of input and processes it like any other functions would do. However, you may have noticed a new keyword ‚Äúexternal‚Äù after our function name. ‚Äúexternal‚Äù means our function can only be called outside of the contract, and not within the contract itself. This is another type of visibility other than ‚Äúpublic‚Äù, however this one is only relative in the context of functions.


Now I know what you‚Äôre thinking: can‚Äôt we just change the message to whatever at any time? Doesn‚Äôt that defeat the whole purpose of ‚Äúdaily‚Äù in daily message? And you‚Äôre correct, so let‚Äôs start implementing some conditions to make sure our contract runs as expected.


At first glance, our function may seem rather ordinary‚Äîit accepts input and processes it, much like any other function. However, there‚Äôs a subtle addition you might have noticed: a new keyword, ‚Äúexternal,‚Äù just after the function name. This ‚Äúexternal‚Äù keyword bestows our function with a unique power‚Äîit can only be invoked from outside the contract, and not from within it. This introduces a new dimension of visibility, distinct from the familiar ‚Äúpublic,‚Äù however this one is only relative in the context of functions.


Now, you might be pondering a curious thought: Can‚Äôt we simply change the message whenever we please? Wouldn‚Äôt that render the concept of a ‚Äúdaily‚Äù message somewhat pointless? Your intuition is spot on, and we‚Äôre about to ensure our contract adheres to its daily mission.


### 5. Setting Some Healthy Boundaries



```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract MessageOfTheDay {
    uint256 public constant MIN_TIME_ELAPSED = 1 days;
    string public message;
    uint256 public messageSetAt;

    constructor(string memory initialMessage) {
        message = initialMessage;
        messageSetAt = block.timestamp;
    }

    function setMessage(string memory newMessage) external {
        if (messageSetAt <= block.timestamp - MIN_TIME_ELAPSED) {
            message = newMessage;
        }
    }
}

```

Wait a minute‚Ä¶ What‚Äôs all of this?!


Hold your horses, let‚Äôs unravel this magical tapestry one thread at a time!


First and foremost, we‚Äôve introduced the ‚ÄúMIN\_TIME\_ELAPSED‚Äù constant, a beacon that guides our contract. This constant, defined with the ‚Äúuint256‚Äù type, signifies a positive integer capable of reaching up to a value of 2256-1. The ‚Äúconstant‚Äù keyword, as it implies, means that our constant shall remain unchanging, engraving it into the contract which increases efficiency.


  
Now, feast your eyes upon ‚Äú1 days,‚Äù a testament to Solidity‚Äôs knack for time-based wizardry. Since blockchain contracts often dance to the rhythm of time, Solidity offers a suite of predefined time-related values, simplifying time-based calculations. Clicking on ‚ÄúMIN\_TIME\_ELAPSED‚Äù reveals its secret: the value 86400, representing the number of seconds in a day.


The ‚ÄúmessageSetAt‚Äù variable is our timekeeper, marking the moment a message is set.


Now, let‚Äôs talk about ‚Äúblock.timestamp.‚Äù It‚Äôs like a cosmic clock that ticks in every blockchain transaction. The ‚Äúblock‚Äù is a global entity accessible by any contract, revealing attributes of the current block in which the contract transaction resides. In simpler terms, when a contract function is called, it operates within a block, and that same block provides essential data.


  
Here, we‚Äôre checking the timestamp of the block‚Äîa number expressing the current time in seconds. If the time when the message was last set is earlier than the current time minus a day, it signals that a full day has elapsed since the previous message. This allows us to usher in a new message.


We‚Äôve woven the threads of basic checking into our contract, but now it‚Äôs time to provide our users with a little more guidance and feedback on the success or failure of their message-setting.


### 6. Spreading The News



```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract MessageOfTheDay {
    error NotEnoughTimePassedSinceLastMessage();
    event NewMessageSet(string message);

    uint256 public constant MIN_TIME_ELAPSED = 1 days;
    string public message;
    uint256 public messageSetAt;

    constructor(string memory initialMessage) {
        message = initialMessage;
        messageSetAt = block.timestamp;
    }

    function setMessage(string memory newMessage) external {
        if (block.timestamp - MIN_TIME_ELAPSED < messageSetAt) {
            revert NotEnoughTimePassedSinceLastMessage();
        }

        message = newMessage;
        emit NewMessageSet(newMessage);
    }
}

```

At the top of our mystical contract, behold the additions we‚Äôve conjured: an ‚ÄúError‚Äù and an ‚ÄúEvent.‚Äù


Errors are companions to the ‚Äúrevert‚Äù keyword, which halts a function‚Äôs execution and undoes any previous changes. We‚Äôve bestowed upon our contract the error ‚ÄúNotEnoughTimePassedSinceLastMessage,‚Äù pretty clear and self-explanatory. You could choose to name it differently, perhaps as ‚ÄúFailedToSetMessage,‚Äù with an explanatory argument if you prefer.


Notice that we‚Äôve tweaked our check condition. It now seeks to catch attempts to set a message less than 24 hours after the previous one. This adjustment allows us to emit an error early in the function, minimizing potential side effects and enhancing performance.


In addition to an error, we‚Äôve introduced an ‚Äúevent.‚Äù Solidity employs events to broadcast changes across the blockchain. Events can carry arguments, and in our case, it carries the new message. Importantly, events serve as a public announcement, eliminating the need for others to manually inspect contract changes.


As a reminder, the blockchain inherently tracks all changes, and anyone can verify whether the contract‚Äôs message has been altered. Events are not record-keepers but rather messengers, efficiently broadcasting updates to all interested parties. [Here‚Äôs something](https://medium.com/coinmonks/learn-solidity-lesson-27-events-f47070b55851) that can help you learn more about them.


Now, let‚Äôs dive into the heart of the matter‚Äîthe enchanting world of [Ether](https://www.investopedia.com/terms/e/ether-cryptocurrency.asp) and the fees for setting our daily message. Get ready for some magical finance!


### 7. Nothing Comes For Free



```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract MessageOfTheDay {
    error NotEnoughTimePassedSinceLastMessage();
    error NotEnoughEther();
    event NewMessageSet(string message);

    uint256 public constant MIN_TIME_ELAPSED = 1 days;
    uint256 public constant MIN_ETHER_SENT = 0.01 ether;
    string public message;
    uint256 public messageSetAt;

    constructor(string memory initialMessage) {
        message = initialMessage;
        messageSetAt = block.timestamp;
    }

    function setMessage(string memory newMessage) external payable {
        if (block.timestamp - MIN_TIME_ELAPSED < messageSetAt) {
            revert NotEnoughTimePassedSinceLastMessage();
        }
        if (msg.value < MIN_ETHER_SENT) {
            revert NotEnoughEther();
        }

        message = newMessage;
        emit NewMessageSet(newMessage);
    }
}

```

Let‚Äôs break down this enchanted transformation, step by step:


First, we‚Äôve summoned two new constants: ‚ÄúMIN\_ETHER\_SENT‚Äù and the accompanying error ‚ÄúNotEnoughEther.‚Äù These define the minimum amount of Ether required for a successful function call and inform users if their offering falls short.


Next, there‚Äôs the ‚Äúpayable‚Äù spell. This tags our function as capable of receiving Ether‚Äîa necessity when dealing with money in Solidity. It‚Äôs essential to note that for a function to accept Ether, it must also be marked as ‚Äúexternal‚Äù or ‚Äúpublic.‚Äù Lucky for us, this is already the case.


Now, the ‚Äúmsg‚Äù keyword enters the stage. Like the ‚Äúblock‚Äù we‚Äôve introduced earlier, when a user calls a function, the ‚Äúmsg‚Äù emerges as a global variable, bearing vital information about the call to this function. Among its attributes, the most used are ‚Äúvalue‚Äù (the amount of Wei sent) and ‚Äúsender‚Äù (the sender‚Äôs address).


To clarify, Wei is a smaller denomination of Ether, ideal for microtransactions. You can convert between Wei and Ether by dividing or multiplying by 1018 , or using an [online conversion tool.](https://eth-converter.com/)


Phew‚Äîeverything is set. Or is it‚Ä¶


Given the public nature of blockchain contracts, anyone can tamper with the message. And right now our Ether is locked behind the bars of the contract‚Äôs chamber. We desire instant message-changing abilities and the power to withdraw Ether, but these privileges must be held only by a trusted user.


The answer? We introduce a privileged user. This user, upon deploying the contract, becomes the gatekeeper of these extraordinary powers.


### 8. All Hail The King



```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract MessageOfTheDay {
    error NotEnoughTimePassedSinceLastMessage();
    error NotEnoughEther();
    error NotAuthorized();
    event NewMessageSet(string message);

    uint256 public constant MIN_TIME_ELAPSED = 1 days;
    uint256 public constant MIN_ETHER_SENT = 0.01 ether;
    address public immutable owner;
    string public message;
    uint256 public messageSetAt;

    constructor(string memory initialMessage) {
        message = initialMessage;
        messageSetAt = block.timestamp;
        owner = msg.sender;
    }

    modifier onlyOwner() {
        if (msg.sender != owner) {
            revert NotAuthorized();
        }
        _;
    }

    function setMessage(string memory newMessage) external payable {
        if (block.timestamp - MIN_TIME_ELAPSED < messageSetAt) {
            revert NotEnoughTimePassedSinceLastMessage();
        }
        if (msg.value < MIN_ETHER_SENT) {
            revert NotEnoughEther();
        }

        message = newMessage;
        emit NewMessageSet(newMessage);
    }

    function forceSetMessage(string memory newMessage) external onlyOwner {
        message = newMessage;
        emit NewMessageSet(newMessage);
    }
}

```

As you might have guessed, it‚Äôs time to crown an owner for the contract, a guardian with administrative powers. And who could that be? Well, it‚Äôs none other than you! It‚Äôs a customary practice for the person deploying the contract to also become its rightful owner.


In this enchanted scroll, behold the following transformations:


We‚Äôve introduced a key character: the ‚Äúowner‚Äù of the contract. This noble entity is assigned an ‚Äúaddress‚Äù type, which is instrumental in tracking accounts or wallets on the blockchain. The ‚Äúimmutable‚Äù attribute ensures that this variable must be set during the contract‚Äôs creation (the constructor) and remains unaltered thereafter.


The constructor ritual bestows ownership upon the deployer. The ‚Äúowner‚Äù is set to ‚Äúmsg.sender.‚Äù The ‚Äúmsg.sender‚Äù represents the address calling the function. In this case, in the context of the constructor, it‚Äôs the address of the person deploying the contract (you!), making you the esteemed owner.


Enter the ‚Äúmodifier,‚Äù a mystical cloak that wraps around other functions. We‚Äôve applied the ‚ÄúonlyOwner‚Äù modifier to the ‚ÄúforceSetMessage‚Äù function, ensuring that only the owner possesses the privilege to invoke it.


Modifiers, just like functions, can accept arguments, which weren‚Äôt a necessity for us. The underscore ‚Äú\_‚Äù is also a special symbol in modifiers. It represents a placeholder where the actual function code will be inserted. When ‚ÄúforceSetMessage‚Äù is called, the code in ‚ÄúonlyOwner‚Äù is executed first, and then control is passed to the function. The \_; allows this transition.


The modifier also has some interesting properties of its own. Firstly just like a function, you can give it arguments that can be passed when it‚Äôs being added to a function, but we didn‚Äôt need to here. Secondly, you can see that it ends with an underscore, which represents the code where the wrapped function should run. In this case, the ‚ÄúforceSetMessage‚Äù runs after the modifier does, because the underscore is placed after the modifier code.


The placement of the underscore within a modifier can vary based on your specific requirements. You have the flexibility to position it before the modifier‚Äôs code or even within the middle of the modifier‚Äôs code, creating what we might whimsically call a ‚Äúmodifier sandwich.‚Äù


Now, moving on to the final piece of the puzzle, we shall exile all those who have written malicious messages.


### 9. You‚Äôre Not Welcome Here



```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract MessageOfTheDay {
    error NotEnoughTimePassedSinceLastMessage();
    error NotEnoughEther();
    error NotAuthorized();
    event NewMessageSet(string message);

    uint256 public constant MIN_TIME_ELAPSED = 1 days;
    uint256 public constant MIN_ETHER_SENT = 0.01 ether;
    address public immutable owner;
    string public message;
    uint256 public messageSetAt;
    mapping(address => bool) private _bannedAddresses;

    constructor(string memory initialMessage) {
        message = initialMessage;
        messageSetAt = block.timestamp;
        owner = msg.sender;
    }

    modifier onlyOwner() {
        if (msg.sender != owner) {
            revert NotAuthorized();
        }
        _;
    }

    function setMessage(string memory newMessage) external payable {
        if (block.timestamp - MIN_TIME_ELAPSED < messageSetAt) {
            revert NotEnoughTimePassedSinceLastMessage();
        }
        if (msg.value < MIN_ETHER_SENT) {
            revert NotEnoughEther();
        }
        if (_bannedAddresses[msg.sender]) {
            revert NotAuthorized();
        }

        message = newMessage;
        emit NewMessageSet(newMessage);
    }

    function forceSetMessage(string memory newMessage) external onlyOwner {
        message = newMessage;
        emit NewMessageSet(newMessage);
    }

    function banAddress(address addressToBan) external onlyOwner {
        _bannedAddresses[addressToBan] = true;
    }
}

```

In the mystical world of smart contracts, we introduce a powerful spellbook known as a ‚Äúmapping.‚Äù This dictionary of spells allows us to associate addresses with secret boolean spells, where ‚Äútrue‚Äù signifies a mystical banishment, and ‚Äúfalse‚Äù represents freedom.


At the start, this spellbook is blank, and all addresses wander freely. However, the wizardly ‚ÄúbanAddress‚Äù function bestows the owner with the ability to impose a ban upon any address. By invoking this function, the owner marks an address as bewitched, setting its corresponding spell in the mapping to ‚Äútrue.‚Äù


Now, as mere mortals interact with the ‚ÄúsetMessage‚Äù spell, a subtle check transpires. If an address has fallen under the spell of banishment, an error¬† materializes, preventing any further magic.


A magical note to ponder: While we‚Äôve concealed the ‚Äú\_bannedAddresses‚Äù mapping from prying eyes by marking it as ‚Äúprivate‚Äù within the contract‚Äôs enchanted realm, remember that the blockchain unveils all secrets to those who seek them. Thus, external observers can discern the banishment status of addresses.


However, contracts wield their own spells and can communicate with one another. This is something we shall explore in another journey. For the purpose of this one, all the knowledge you need to know it that ‚Äúprivate‚Äù variables are invisible between contracts, and only between contracts.


We‚Äôve arrived at the grand finale of our mystical journey through the enchanted realm of Solidity and smart contracts.


Hold! We overlooked a step you say?


Well, not at all! The enchanting truth about this last step is that it‚Äôs a blank canvas awaiting your magical touch.


The Master of the Chain
-----------------------


In the enchanted realm of Ethereum, you have the power to add more features to your contract. Consider these possibilities:


1. **Admin Council**: Create a group of trusted individuals with special privileges to manage the contract when you‚Äôre not available. They can be like a council of wizards who help oversee the contract‚Äôs affairs.
2. **Bidding War:** Introduce a system where users compete by bidding with their Ether to earn the right to set the daily message. The user with the highest bid will be able to decide the message of the day.
3. **Vote for the Message**: Implement a voting system where users can cast their votes for their favorite messages. The message with the most votes will be chosen as the daily message, giving the community a voice in deciding the message.


Remember, in the world of Ethereum, you have the ability to add new features and engage the community. Use your programming skills to bring these ideas to life and shape the destiny of your contract.


May your journey in the world of smart contracts be filled with wonder and endless possibilities. Good luck, brave enchanter!



![](https://metana.io/wp-content/uploads/2023/11/FAQs-cuate-1-1024x1024.png)
1. **How do you write a Solidity smart contract?**


Writing a Solidity smart contract involves several steps. First, you need to install the solidity compiler (solc). Once you‚Äôve done that, you can start writing the contract. Here‚Äôs a basic sample of a Solidity contract:



```
pragma solidity ^0.5.16;

contract SimpleStorage {

¬†¬†¬†¬†uint data;

¬†¬†¬†¬†function set(uint x) public {

¬†¬†¬†¬†¬†¬†¬†¬†data = x;

¬†¬†¬†¬†}

¬†¬†¬†¬†function get() public view returns (uint) {

¬†¬†¬†¬†¬†¬†¬†¬†return data;

¬†¬†¬†¬†}

}

//This simple contract has a single variable, `data`, and two functions, `set` and `get`, that set and retrieve the value of `data` respectively.
```


2. **Are smart contracts written in Solidity?**


Yes, many smart contracts, especially those on the Ethereum blockchain, are written in Solidity. However, Solidity isn‚Äôt the only language for writing smart contracts. Other languages, such as Vyper for Ethereum, Chaincode for Hyperledger Fabric, and Rust and C++ for EOS, are also used.


3. **How do you write a smart contract on Ethereum in Solidity?**


Writing a smart contract for Ethereum in Solidity involves writing the contract in the Solidity language and then deploying it to the Ethereum blockchain. The deployment process typically involves compiling the contract to bytecode using the Solidity compiler and then sending a transaction to the Ethereum network that includes the compiled contract.


4. **How do you write a smart contract?**


Writing a smart contract involves defining the rules and protocols for performing transactions in a programmable format. This programming is done using specific languages like [Solidity](https://metana.io/blog/solidity-the-programming-language-of-decentralized-applications/), Vyper, and others. Once written, the contract is deployed to a blockchain network where it can be interacted with.


5. **Is it easy to write smart contracts?**


The difficulty of writing smart contracts largely depends on your knowledge of programming and the specific language used. If you are familiar with the programming concepts and the language, it can be relatively straightforward. However, due to the immutable and transparent nature of the blockchain, writing safe and secure smart contracts can be challenging.


6. **How do I write my first smart contract?**


To write your first smart contract, start by learning a smart contract language such as Solidity. Then, write a simple contract and deploy it to a test Ethereum network using tools like Truffle or Hardhat.¬†


7. **Are smart contracts written in code?**


Yes, smart contracts are written in code. They are essentially programs that execute on a blockchain.


8. **How do I write my own contract?**


To write your own contract, you need to learn a smart contract language like Solidity, define the rules of the contract, and then write those rules in code. Afterward, compile the contract and deploy it to a blockchain.


9. **What language are smart contracts written in?**


Smart contracts are typically written in languages such as Solidity and Vyper for Ethereum, Chaincode for Hyperledger Fabric, and others like Rust and C++ for EOS.


10. **Should I learn Solidity in 2023?**


The decision to learn Solidity in 2023 depends on your goals. If you aim to develop on the Ethereum blockchain, then learning Solidity is highly recommended, as it is the main language for Ethereum smart contracts. However, it would be beneficial also to evaluate other blockchain platforms and their languages.






![How-to-Write-Solidity-Smart-Contracts](https://metana.io/wp-content/uploads/2023/10/How-to-Write-Solidity-Smart-Contracts-3.jpg) 





[PrevPreviousHow to Make a Flash Loan Using Aave](https://metana.io/blog/how-to-make-a-flash-loan-using-aave/) 




[NextSolidity Variables: Types and UsesNext](https://metana.io/blog/solidity-variables-types-and-uses/) 







#### Metana Guarantees  a Job üíº

 





#### Plus Risk Free 2-Week Refund Policy ‚ú®

 




 You‚Äôre guaranteed a new job in web3‚Äîor you‚Äôll get a full tuition refund. We also offer a hassle-free two-week refund policy. If you‚Äôre not satisfied with your purchase for any reason, you can request a refund, no questions asked.

 






Web3 Solidity Bootcamp
======================

 





 The most advanced Solidity curriculum on the internet!

 




* 4 Months
* Prior coding experience required
* 20h/ Week
* 1-on-1 mentorship
* Expert code reviews
* Coaching & career services






[View Program](/web3-solidity-bootcamp-ethereum-blockchain/)







Full Stack Web3 Beginner Bootcamp
=================================

 





 Learn foundational principles while gaining hands-on experience with Ethereum, DeFi, and Solidity.

 




* 7 Months
* Beginner - Zero to Hero
* 25h/ Week
* Your very own personal support tutor
* 1-on-1 mentorship
* Expert code reviews
* Coaching & career services






[View Program](/web3-beginner-bootcamp/)













You may also like
-----------------

 






![](https://metana.io/wp-content/uploads/2024/07/Protect-Yourself-from-Rug-Pulls-Tips-to-Avoid-Crypto-Scams-640x364.jpg)







### [Metana Editorial](https://metana.io/blog/author/editorial/)




4 mins ago

#### [Protect Yourself from Rug Pulls: Tips to Avoid Crypto Scams](https://metana.io/blog/protect-yourself-from-rug-pulls-tips-to-avoid-crypto-scams/)




The world of Web3 is brimming with promises of high returns and revolutionary projects, driving 


![](https://metana.io/wp-content/uploads/2024/07/ERC-Token-Standards-Your-Simplified-Guide-copy-640x364.jpg)







### [Metana Editorial](https://metana.io/blog/author/editorial/)




1 day ago

#### [Address Poisoning in Smart Contracts](https://metana.io/blog/address-poisoning-in-smart-contracts/)




Web3 thrives on user empowerment and the ease of sending and receiving cryptocurrency. However, a 
 







#### Metana Guarantees  a Job üíº

 





#### Plus Risk Free 2-Week Refund Policy

 




You‚Äôre guaranteed a new job in web3‚Äîor you‚Äôll get a full tuition refund. We also offer a hassle-free two-week refund policy. If you‚Äôre not satisfied with your purchase for any reason, you can request a refund, no questions asked.

 






#### Web3 Solidity Bootcamp

 





 The most advanced Solidity curriculum on the internet

 




* 4 Months
* Prior coding experience required
* 20h/ Week
* 1-on-1 mentorship
* Expert code reviews
* Coaching & career services






[View Program](/web3-solidity-bootcamp-ethereum-blockchain/)







Full Stack Web3 Beginner Bootcamp
=================================

 





 Learn foundational principles while gaining hands-on experience with Ethereum, DeFi, and Solidity.

 




* 7 Months
* Beginner - Zero to Hero
* 25h/ Week
* Your very own personal support tutor
* 1-on-1 mentorship
* Expert code reviews
* Coaching & career services







 Learn foundational principles while gaining hands-on experience with Ethereum, DeFi, and Solidity.

 




[View Program](/web3-beginner-bootcamp/)







Events by Metana
================

 





 Dive into the exciting world of Web3 with us as we explore cutting-edge technical topics, provide valuable insights into the job market landscape, and offer guidance on securing lucrative positions in Web3. 

 




 





 







Do you have anything you want us to cover in our blog posts? What should we talk about next? We need your suggestions! 
 





Submit
















Start Your  Application
-----------------------

 






Secure your spot now. Spots are limited, and we accept qualified applicants on a first come, first served basis..




 






Email(Required)

Career Track(Required)


Web3



Data



Full Stack



Cyber

  








Œî










The application is free and takes just 3 minutes to complete.

 















##### What is included in the course?

 






### Expert-curated curriculum










### Weekly 1:1 video calls with your mentor










### Weekly group mentoring calls










### On-demand mentor support










### Portfolio reviews by Design hiring managers










### Resume & LinkedIn profile reviews










### Active online student community










### 1:1 and group career coaching calls










### Access to our employer network










### Job Guarantee




















 






###### Address

 





 44 Montgomery St,   

San Francisco, CA 94104, United States

 












Email 







Subscribe












###### Contact us

 





###### Call us directly

 





 (415) 416-0800

 





###### Mail us directly

 





 [[email¬†protected]](/cdn-cgi/l/email-protection)

 









### Company

 




* [Home](/)
* [Blog](https://metana.io/blog/)
* [Jobs at Metana](/jobs/)
* [Bootcamp Application](https://metana.io/apply/)
* [Refer a friend](/refer/)
* [Student Perks](/perks/)
* [Testimonials](/reviews)
* [LMS login](https://app.metana.io/)
* [Open LMS login (Full Stack)](https://open.metana.io/)









### Bootcamp

 




* [Web3 Bootcamps](https://metana.io/web3-0-bootcamp/)
* [- Solidity Bootcamp](https://metana.io/web3-solidity-bootcamp-ethereum-blockchain/)
* [- Web3 Beginner Bootcamp](https://metana.io/web3-solidity-bootcamp-ethereum-blockchain/)
* [- Rust Bootcamp](https://metana.io/web3-rust-bootcamp-solana-blockchain/)
* [- ZK Bootcamp](/web3-zero-knowledge-bootcamp/)
* [Data + AI bootcamps](#)
* [- Data Analytics Bootcamp](/data-analytics-bootcamp/)
* [- Data Science Bootcamp](/data-science-bootcamp/)
* [- AI/ML Bootcamp](/ai-machine-learning-bootcamp/)
* [Software Engineering Bootcamp](/full-stack-software-engineer-bootcamp/)
* [Cybersecurity Bootcamp](/cybersecurity-bootcamp/)
* [Jobcamp](/jobcamp/)
* [Tuition](https://metana.io/tuition/)









### For businesses

 




* [Business](https://metana.io/business/)
* [- Hiring Partner](https://metana.io/business/hiring-partner/)
* [- Upskill Your Team](/business/upskill-your-team/)









### Legal

 




* [Licences](https://metana.io/licences/)
* [Metana's Job Guarantee](https://metana.io/job-guarantee/)
* [Withdrawal and Refund Policy](https://metana.io/withdrawal-and-refund-policy/)
* [Privacy Policy](/privacy-policy/)













[Still Unsure?](https://formless.ai/c/6bPi6ASGJ4ge)








[![](https://metana.io/wp-content/uploads/2022/07/Metana-Logo-Black-Full.png)](https://metana.io) 







 Disclaimer:
‚Äç
Metana is involved in the Blockchain Education activities only. We are not dealing with any Crypto related stuff nor provide advice in the Crypto related field.

 

















 Metana is a pioneer in education and career transformation, specializing in today‚Äôs most in-demand skills. The leading source for training, staffing, and career transitions, we foster a flourishing community of professionals pursuing careers they love.
| ¬© 2023 Edmore (private) limited

 








[Facebook](https://www.facebook.com/metanahq) 





[Linkedin](https://www.linkedin.com/school/metana/) 














































































Adding {{itemName}} to cart


Added {{itemName}} to cart













Loading...



√ó






